env() {

    if (global.env == void) global.env = this;

    help() {
        for ( currentline : this.help ) {
          print(currentline);
        }
        getBshPrompt();
    }

    this.help = new ArrayList();
    help.add("");
    help.add("╒════════════════════════════════");
    help.add("│ Defining env() ...");
    help.add("│════════════════════════════════");
    help.add("│ ");
    help.add("│ Methods: ");

    help.add("│   d() ... display() ");
    display() {
        variables();
        methods();
        // methodnames();
        print("Note: Use methodnames() to get *just* method names\n");
    }

    // Alias for commonly used display() method
    d() { display(); }

    help.add("│   v() ... variables() ");
    variables() {
        print("╒════════════════════════════════");
        print("│   Variables");
        print(" ════════════════════════════════");
        sortandprint(global.namespace.getVariableNames());
    }

    // Alias for commonly used variables() method
    v() { variables(); }

    help.add("│   m() ... methods()");
    methods() {
        print("╒════════════════════════════════");
        print("│   Methods ");
        print(" ════════════════════════════════");
        print(global.namespace.getMethods());
    }

    // Alias for commonly used methods() method
    m() { methods(); }

    help.add("│   methodnames()");
    methodnames() {
        print("╒════════════════════════════════");
        print("│   Method Names ");
        print(" ════════════════════════════════");
        sortandprint(global.namespace.getMethodNames());
    }

    help.add("│   sortandprint*(String[])");
    sortandprint(String[] sortme) {
        Arrays.sort(sortme);
        for ( currentvar : sortme ) {
          print(currentvar);
        }
    }

    help.add("│ ");
    help.add("│════════════════════════════════");
    help.add("│ Reminder: env = env()");
    help.add(" ════════════════════════════════");

    this.help();
    this.display();

    return this;
}

// env = env();
